plugins {
    id 'java'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.18.0'
    id 'groovy'
    id 'java-gradle-plugin'
}

group "com.bsycorp.gradle"
if (findProperty("appVersion") != null){
    version = findProperty("appVersion")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    api("com.google.cloud.tools:jib-core:0.20.0")
    implementation("com.google.guava:guava:30.1.1-jre")

    testImplementation gradleTestKit()
    testImplementation("org.junit.jupiter:junit-jupiter:5.7.1")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

pluginBundle {
    website = 'https://github.com/bsycorp/gradle-jib-plugin'
    vcsUrl = 'https://github.com/bsycorp/gradle-jib-plugin.git'
    tags = ['jib', 'docker', 'container']
}

gradlePlugin {
    plugins {
        jibPlugin {
            id = "io.github.bsycorp.jib"
            displayName = ""
            description = ""
            implementationClass = "com.bsycorp.gradle.jib.JibPlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/bsycorp/gradle-jib-plugin")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}